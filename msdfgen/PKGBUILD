#Maintainer: Fredrik Andersson <fredrik.ks.andersson@gmail.com>
pkgname=msdfgen-git
pkgver=r85.1b2bee7
pkgrel=1
pkgdesc="Multi-channel signed distance field generator"
arch=("x86_64")
license=("MIT")
makedepends=("git" "cmake" "make")
depends=("freetype2")
source=("git+https://github.com/Chlumsky/msdfgen.git")
md5sums=("SKIP")

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cd "$srcdir/${pkgname%-git}"

  mkdir _build && cd _build
  cmake -DCMAKE_BUILD_TYPE=Release ..

  make
}

package() {
  cd "$srcdir/${pkgname%-git}"/_build

  make DESTDIR="$pkgdir/" install
}
